sonarqube {
  properties {
    property "sonar.projectName", "${projectTitle} :: VSTS"
    property "sonar.sources", "src/main/js"
    property "sonar.tests", "src/main/js"
    property "sonar.test.inclusions", "src/main/js/**/__tests__/**"
    property "sonar.exclusions", "src/main/js/libs/third-party/**/*,src/main/js/**/__tests__/**"
  }
}

apply plugin: 'com.moowork.node'

node {
  version = '8.5.0'
  yarnVersion = '1.1.0'
  download = true
}

yarn_run {
  inputs.dir('config')
  inputs.dir('public')
  inputs.dir('scripts')
  inputs.dir('src/main/js')

  inputs.file('.babelrc')
  inputs.file('package.json')
  inputs.file('tsconfig.json')
  inputs.file('yarn.lock')

  outputs.dir('build/webapp')

  args = ['build']
}

task zipVSTS(type: Zip, dependsOn: yarn_run) {
  into("integration/vsts/") {
    from file('build/webapp')
  }
}

configurations { zip }
artifacts { zip zipVSTS }
